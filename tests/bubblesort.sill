ltype BS =  &{pop   : +{none:1; some: Int /\ BS};
              round : BS };;

(* 1: Doesn't look like we'll want global types
   2: We will need non-uniform recursion on session types.
      However, the non-uniform bits are only at the dependent level.
   3: Should the ltype definition even have dependency? *)

(* We want to prove that, ideally, sort ran for the right number of rounds
   returns sorts the elements of BS. This is too hard, so we'll try to confirm
   that it represents a permutation of the original elements instead. *)

(* First we need a notion of BS that is constrainable by our predicates.
   I'm not sure what the best notation for this is, but I'll use BS(X) for its
   extra logical parameter. Specifically, we'll want to track the multiset of
   elements that are contained in BS, so BS(X) will just assume X is a multiset.
   The fully spelled out version should be something like {x:BS|F(x)} *)

(* Lines that add interesting constraints are listed by where they're generated. *)

(* The definition of BS(x,X) would look something like 
      ltype BS(x,X) = &{pop   : +{none:1; some: x:Int/\ BS(X)};
                      round : BS(x,X)};; *)

(* empty: {BS(\emptyset)} *)
(* subtyping means that this could be BS(X) for any X *)
empty : {BS};; 
'c <- empty = (* 'c::{BS | A} *)
  case 'c of
  | pop -> 'c.none;
           close 'c
  | round -> 'c <- empty;; (* 'c::{BS | B} and A ==> B *)

(* elem : x:Int -> {BS(x,X) <- BS(X)} *)
elem : Int -> {BS <- BS};;
'c <- elem i -< 'd =   (* 'd::{BS | B(x)} and 'c::{BS | A(x)} *)
  case 'c of
  | pop -> 'c.some;
           send 'c i;  (* 'd :: {BS | C(x,i)} *)
           'c <- 'd    (* C(i) ==> A *)
  | round -> 'd.round; (* 'd :: {BS | D(x)} *)
             'd.pop;
             case 'd of
             | none -> wait 'd;
                       'e <- empty;
                       'c <- elem i -< 'e (* F(x) &&& empty ==> A(x) *)
             | some -> (* 'd :: {j:Int /\ BS | F(j)} *)
                       x <- recv 'd; (* 'd :: {BS | F(j) } *)
                       if i > x 
                       then (* j>x *)
                            'e <- elem i -< 'd; (* j>x &&& F(j) ==> 'e : {BS | A(j)} *)
                            'c <- elem x -< 'e (*  (i>x &&& F(j) &&& A(j) ==> A(x))
                                               &&& A(x) ==> A(x) *)
                       else (* j<= x *)
                            'e <- elem x -< 'd; (* j<=x &&& F(j) ==> 'e::A(x)
                            'c <- elem i -< 'e;; (*  j<=x &&& F(j) &&& 'e::A(x) ==> 'c::A(i) 
                                                 &&& 'c::A(i) ==> 'c::A(x) *)

(* What we can say here depends a lot on our predicate templates. 
   If we can convert lists to multisets we're fine. *)
fromList : [Int] -> {BS};;
'c <- fromList l =
  case l of
  | [] -> 'c <- empty
  | h::t -> 'd <- fromList t;
            'c <- elem h -< 'd;;

printBS : {1 <- BS};;
'c <- printBS -< 'd =
  'd.pop;
  case 'd of
  | none -> wait 'd;
            close 'c
  | some -> x <- recv 'd;
            print x;
            'c <- printBS -< 'd;;

'top <- 'c <- fromList [4;5;9;6;5;7;8];
        'd <- printBS -< 'c;
        wait 'd;
        print_str "\n";
        close 'top;;

(* sort: n:Int -> {BS(X) <- BS(X)} *)
sort : Int -> {BS <- BS};;
'c <- sort n -< 'd =
  if n = 0
  then 'c <- 'd
  else 'd.round;
       'c <- sort (n-1) -< 'd;;

'top <- 'c <- fromList [4;5;9;6;5;7;8];
        'c <- sort 7 -< 'c;
        'd <- printBS -< 'c;
        wait 'd;
        print_str "\n";
        close 'top;;
